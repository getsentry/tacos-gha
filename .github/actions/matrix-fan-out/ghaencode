#!/usr/bin/env -S sed -zrf
# Allow for arbitrary characters to be passed through gha artifact names, via
# an escaping scheme. This means we can use user inputs as part of our artifact
# names while still writing no code whatsoever to handle weird user inputs
# breaking the system.
#
# All escapes start with `$` (`$` is escaped as `$$`), with one special case:
# `/` is escaped to ` `, allowing escaped file paths to be quite readable. (` `
# is escaped to `$ `.)

# reference:
# actions/upload-artifact notes: Invalid characters include: ":<>|*?\r\n\/
# in other words, valid characters include: ' `~!@#$%^&()_+{}-=[];,.
# unused escapes: ~@#`'()-=[]

# our escape character is $
s \$ \$\$ g
s \" \$\^ g
s \: \$\; g
 s < \$\{ g
 s > \$\} g
s \| \$\+ g
s \* \$\& g
s \? \$\! g
s \r \$\, g
s \n \$\. g
s \\ \$\% g

# escape ` ` so we can use a space to denote a slash:
s|\ |\$\_|g
s|\/|\ |g
